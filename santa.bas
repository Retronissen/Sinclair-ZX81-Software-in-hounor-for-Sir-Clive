   2 LET HISCORE=0
   3 LET TRY=6
   5 GOTO 1000
   6 LET X=10
   7 LET Y=3
   8 LET SCORE=0
   9 CLS
  10 LET A=INT (RND*32)-12
  11 LET TRY=TRY-1
  12 IF TRY=0 THEN GOTO 2000
  15 LET PIPE=A+6
  18 PRINT AT 0,0;"%S%A%N%T%A\::%S%I%M%U%L%A%T%O%R\::%1%.%0 ";"SCORE:";SCORE
  20 PRINT AT 15,A;"     \ : \: "
  30 PRINT AT 16,A;"  \.'\''\''\''\''\''\''\''\'."
  40 PRINT AT 17,A;" \.'\:        \ :\'."
  50 PRINT AT 18,A;"\.' \:  \:'\': \:'\': \ : \'."
  60 PRINT AT 19,A;"  \:  \:.\.: \:.\ : \ :"
  70 PRINT AT 20,A;"  \:     \: \ : \ :"
  80 PRINT AT 21,A;"\,,\##\;;\;;\;;\;;\;;\;;\;;\;;\;;\##\,,"
 100 IF INKEY$ ="Q" THEN LET Y=Y-1
 110 IF INKEY$ ="A" THEN LET Y=Y+1
 120 IF INKEY$ ="O" THEN LET X=X-1
 130 IF INKEY$ ="P" THEN LET X=X+1
 140 IF INKEY$ ="9" THEN GOTO 600
 150 IF INKEY$ ="0" THEN GOTO 700
 190 IF Y<2 THEN LET Y=2
 191 IF Y>12 THEN LET Y=12
 192 IF X<0 THEN LET X=0
 193 IF X>18 THEN LET X=18
 198 PRINT AT Y-1,X;"                 "
 199 PRINT AT Y+2,X;"                 "
 200 PRINT AT Y,X;" \'.\..\.' \. \'.\: \,,\@@\,,\'. "
 210 PRINT AT Y+1,X;" \' \: \:  \'.\..\:.\!!\!!\!!\'  "
 220 GOTO 100
 600 PRINT AT Y-1,X+6;"\##"
 601 PRINT AT Y-1,X+5;"\## "
 602 PRINT AT Y-1,X+4;"\## "
 603 PRINT AT Y-1,X+3;"\## "
 604 PRINT AT Y-1,X+2;"\## "
 605 PRINT AT Y-1,X+1;"\## "
 606 PRINT AT Y-1,X;"\## "
 607 PRINT AT Y-1,X;"  "
 608 LET HIT=X
 610 FOR D=Y TO 13
 630 LET HIT=HIT-INT (RND*2)
 632 IF HIT<0 THEN LET HIT=0
 634 IF HIT>31 THEN LET HIT=31
 635 PRINT AT D,HIT;"\##"
 636 PRINT AT D,HIT;" "
 640 NEXT D
 650 GOTO 800
 700 PRINT AT Y-1,X+7;"\##"
 701 PRINT AT Y-1,X+7;" \##"
 702 PRINT AT Y-1,X+8;" \##"
 703 PRINT AT Y-1,X+9;" \##"
 704 PRINT AT Y-1,X+10;" \##"
 705 PRINT AT Y-1,X+11;" \##"
 706 PRINT AT Y-1,X+11;"  "
 708 LET HIT=X+12
 710 FOR D=Y TO 13
 730 LET HIT=HIT+INT (RND*2)
 732 IF HIT<0 THEN LET HIT=0
 734 IF HIT>31 THEN LET HIT=31
 735 PRINT AT D,HIT;"\##"
 736 PRINT AT D,HIT;" "
 740 NEXT D
 750 GOTO 800
 800 LET BONUS=((22-Y)*(22-Y))*10
 804 IF HIT=PIPE THEN LET SCORE=SCORE+BONUS
 805 IF HIT=PIPE THEN GOTO 812
 806 FOR G=1 TO 8
 808 PRINT AT Y+3,X+1;"%Y%O%U\::%M%I%S%S%E%D"
 810 NEXT G
 811 IF HIT<>PIPE THEN GOTO 9
 812 FOR G=1 TO 6
 813 PRINT AT 14,PIPE;"\##"
 814 PRINT AT 14,PIPE;" "
 815 NEXT G
 816 FOR G=1 TO 8
 817 PRINT AT 15,PIPE;"\##"
 818 NEXT G
 819 PRINT AT 15,PIPE;" "
 820 GOTO 9
1000 CLS
1001 PRINT " WELCOME TO SANTA SIMULATOR 1.0"
1005 PRINT "\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''"
1007 PRINT AT 3,15;"BY"
1010 PRINT AT 4,4;"KURT-ARNE JOHNSEN, 2021"
1030 PRINT AT 7,0;"%C%O%N%T%R%O%L%S:"
1040 PRINT "Q/A = UP/DOWN"
1050 PRINT "O/P = LEFT/RIGHT"
1060 PRINT "9/0 = THROW PRESENT LEFT/RIGHT"
1070 PRINT AT 13,0;"YOU ARE SANTA AND YOUR MISSION  IS TO STEER THE DEER. POSITION  RUDOLPH AND THE SLEIGH IN THE   SKY AND THROW PRESENTS THROUGH  CHIMNEYS. HIGHER ALTITUDE GIVES HIGHER BONUS. IT IS ANNOYINGLY  HARD. MERRY CHRISTMAS TO YOU."
1080 PRINT AT 21,10;"PRESS ANY KEY"
1090 IF INKEY$ ="" THEN GOTO 1090
1100 GOTO 6
2000 CLS
2010 PRINT AT 6,4;"GAME OVER. YOUR SCORE:";SCORE
2013 IF SCORE>HISCORE THEN PRINT AT 8,10;"%N%E%W\::%H%I%G%H%S%C%O%R%E"
2015 IF SCORE>HISCORE THEN LET HISCORE=SCORE
2017 PRINT AT 10,10;"HIGHSCORE:";HISCORE
2020 PRINT AT 21,10;"PRESS ANY KEY"
2030 IF INKEY$ ="" THEN GOTO 2030
2040 IF INKEY$ <>"" THEN GOTO 3
