   2 REM MINEHUNT V1.12
   3 LET LVL=1
   4 LET CHEATS=0
   5 GOSUB 7000
   6 LET MINES=LVL*5+5
   7 LET SC=0
   8 FAST
  10 DIM B(12,22)
  11 FOR N=1 TO 10 STEP 2
  12 PRINT AT N,1;"\##\@@\##\@@\##\@@\##\@@\##\@@\##\@@\##\@@\##\@@\##\@@\##\@@";AT N+1,1;"\@@\##\@@\##\@@\##\@@\##\@@\##\@@\##\@@\##\@@\##\@@\##\@@\##"
  14 NEXT N
  16 PRINT AT 0,22;"%-%M%I%N%E%H%U%N%T%-"
  17 FOR N=1 TO 11
  18 PRINT AT N,22;"\::\::\::\::\::\::\::\::\::\::"
  19 NEXT N
  20 DIM A(12,22)
  30 FOR N=1 TO MINES
  40 LET X=INT (RND*20)+1
  50 LET Y=INT (RND*10)+1
  52 IF X=1 AND Y=1 THEN GOTO 40
  54 IF X=20 AND Y=10 THEN GOTO 40
  60 IF A(Y+1,X+1)=1 THEN LET N=N-1
  70 IF A(Y+1,X+1)=0 THEN LET A(Y+1,X+1)=1
  80 IF CHEATS=1 THEN PRINT AT Y,X;"*"
  90 NEXT N
 100 PRINT AT 0,0;"\ .\..\..\..\..\..\..\..\..\..\..\..\..\..\..\..\..\..\..\..\..\. "
 102 FOR N=1 TO 10
 104 PRINT AT N,0;"\ :";AT N,21;"\: "
 106 NEXT N
 110 PRINT AT 11,0;"\ '\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\' "
 180 PRINT AT 10,20;"%>"
 182 PRINT AT 13,0;"%C%O%N%T%R%O%L%S%:              "
 184 PRINT AT 15,1;"%Q  %W  %E";AT 16,1;" \'. \:  \.'";AT 17,1;"%A     %D";AT 18,1;" \.' \:  \'.";AT 19,1;"%Z  %X  %C"
 186 PRINT AT 21,0;"\::\::\::%B%Y\::%K%U%R%T%-%A%R%N%E\::%J%O%H%N%S%E%N%,\::%2%0%2%1\::\::\::"
 200 SLOW
 210 LET X=2
 220 LET Y=2
 230 GOSUB 3000
 240 GOSUB 2000
 245 GOSUB 2020
 500 IF INKEY$ ="Q" THEN GOTO 700
 510 IF INKEY$ ="A" THEN GOTO 740
 520 IF INKEY$ ="Z" THEN GOTO 770
 530 IF INKEY$ ="W" THEN GOTO 810
 540 IF INKEY$ ="X" THEN GOTO 850
 550 IF INKEY$ ="E" THEN GOTO 880
 560 IF INKEY$ ="D" THEN GOTO 920
 570 IF INKEY$ ="C" THEN GOTO 950
 590 IF INKEY$ ="" THEN GOTO 500
 600 GOTO 500
 700 IF X>2 AND Y>2 THEN GOTO 720
 710 GOTO 500
 720 PRINT AT Y-1,X-1;" "
 722 LET Y=Y-1
 725 LET X=X-1
 730 GOTO 5000
 740 IF X>2 THEN GOTO 760
 750 GOTO 500
 760 PRINT AT Y-1,X-1;" "
 763 LET X=X-1
 765 GOTO 5000
 770 IF X>2 AND Y<11 THEN GOTO 790
 780 GOTO 500
 790 PRINT AT Y-1,X-1;" "
 793 LET X=X-1
 795 LET Y=Y+1
 800 GOTO 5000
 810 IF Y>2 THEN GOTO 830
 820 GOTO 500
 830 PRINT AT Y-1,X-1;" "
 835 LET Y=Y-1
 840 GOTO 5000
 850 IF Y<11 THEN GOTO 870
 860 GOTO 500
 870 PRINT AT Y-1,X-1;" "
 873 LET Y=Y+1
 875 GOTO 5000
 880 IF X<21 AND Y>2 THEN GOTO 900
 890 GOTO 500
 900 PRINT AT Y-1,X-1;" "
 903 LET Y=Y-1
 905 LET X=X+1
 910 GOTO 5000
 920 IF X<21 THEN GOTO 930
 925 GOTO 500
 930 PRINT AT Y-1,X-1;" "
 935 LET X=X+1
 940 GOTO 5000
 950 IF X<21 AND Y<11 THEN GOTO 960
 955 GOTO 500
 960 PRINT AT Y-1,X-1;" "
 963 LET Y=Y+1
 965 LET X=X+1
 970 GOTO 5000
2000 PRINT AT 2,23;"%M%I%N%E%S%:";MINES
2010 PRINT AT 4,23;"%L%E%V%E%L%:";LVL
2011 PRINT AT 5,23;"%S%C%O%R%E%:";INT (SC/10)
2012 PRINT AT 7,23;"%N%E%A%R%:\::"
2015 RETURN
2020 PRINT AT 7,29;NEAR
2040 PRINT AT Y-1,X-1;"O"
2060 RETURN
3000 LET NEAR=A(Y-1,X-1)+A(Y,X-1)+A(Y+1,X-1)+A(Y-1,X)+A(Y+1,X)+A(Y-1,X+1)+A(Y,X+1)+A(Y+1,X+1)
3050 RETURN
5000 IF A(Y,X)=0 THEN GOTO 5500
5010 PRINT AT 9,25;"BOMB"
5012 GOSUB 5020
5015 GOTO 5048
5020 FAST
5021 FOR N=1 TO 22
5022 FOR F=1 TO 12
5030 LET SC=SC+B(F,N)
5033 IF A(F,N)=1 THEN PRINT AT F-1,N-1;"%*"
5034 NEXT F
5035 NEXT N
5045 PRINT AT 5,29;INT (SC/10)
5046 SLOW
5047 RETURN
5048 FOR N=1 TO 6
5049 PRINT AT Y-1,X-1;"%O";AT Y-1,X-1;"%X"
5050 NEXT N
5051 PRINT AT 14,14;"GAME OVER";AT 15,14;"PLAY AGAIN?"
5060 IF INKEY$ ="Y" THEN RUN 
5065 IF INKEY$ ="N" THEN STOP
5070 GOTO 5060
5500 LET B(Y,X)=1
5510 GOSUB 3000
5520 GOSUB 2020
5530 IF Y=11 AND X=21 THEN GOTO 6000
5540 GOTO 500
6000 PRINT AT 9,25;"YESS"
6001 PRINT AT 14,14;"LEVEL ";LVL;" COMPLETE";AT 15,14;"PRESS ANY KEY"
6002 GOSUB 5020
6010 IF INKEY$ ="" THEN GOTO 6010
6015 LET LVL=LVL+1
6017 LET MINES=MINES+5
6018 CLS
6020 GOTO 8
7000 CLS
7010 PRINT "- WELCOME TO MINEHUNT FOR ZX81 -"
7015 PRINT AT 1,14;"V1.12"
7017 PRINT AT 5,11;"%E - EASY";AT 7,11;"%M - MEDIUM";AT 9,11;"%H - HARD";AT 13,9;"- HAVE FUN -"
7020 IF INKEY$ ="C" THEN LET CHEATS=1
7021 IF INKEY$ ="E" THEN LET LVL=1
7022 IF INKEY$ ="M" THEN LET LVL=3
7023 IF INKEY$ ="H" THEN LET LVL=5
7030 IF INKEY$ ="C" OR INKEY$ ="E" OR INKEY$ ="M" OR INKEY$ ="H" THEN GOTO 7040
7035 GOTO 7020
7040 RETURN
