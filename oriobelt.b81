   2 REM ORIONS BELT V1.01
  10 CLS 
  20 PRINT AT 0,0;"% % % %.%.%.% % % % %O%R%I%O%N%S% %B%E%L%T% % % % % %.%.%.% % % "
  30 PRINT AT 1,0;"\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''"
  40 PRINT AT 3,0;"A DICE GAME FOR 1-8 PLAYERS.    THE GOAL IS TO GET THREE OF A   KIND IN THREE OR LESS THROWS.";AT 7,0;"SCORE:";AT 8,0;"3 X TWOES GIVES 6 POINTS,       3 X THREES GIVES 9 POINTS ETC.  IF YOU THROW THE ORIONS BELT,   3 X ONES, YOU GET 21 POINTS.    THE GAME PLAYS OVER 10 ROUNDS.  HIGHEST TOTAL WINS THE GAME.";AT 15,12;"HAVE FUN"
  50 PRINT AT 17,0;"% % % %1%.%1%.%2%0%2%2% %B%Y% %K%U%R%T%-%A% %J%O%H%N%S%E%N% % % "
  90 DIM T(3)
 110 PRINT AT 21,0;"NUMBER OF PLAYERS (1-8)? ";
 120 INPUT I
 130 IF INT I=I AND I>0 AND I<9 THEN GOTO 200
 140 GOTO 120
 200 CLS 
 202 PRINT AT 0,0;"% % % %.%.%.% % % % %O%R%I%O%N%S% %B%E%L%T% % % % % %.%.%.% % % "
 205 PRINT AT 1,0;"\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''\''"
 206 DIM S$(I,3)
 210 DIM S(I,11)
 230 FOR N=1 TO I
 240 PRINT AT 21,0;"NAME PLAYER ";N;", MAX 3 LETTERS"
 250 INPUT N$
 260 LET S$(N)=N$
 270 NEXT N
 280 PRINT AT 21,0;"                                "
 500 FOR N=1 TO I
 505 LET S(N,11)=N*4-4
 510 PRINT AT 3,S(N,11);S$(N)
 520 NEXT N
1000 FOR R=1 TO 10
1010 FOR N=1 TO I
1050 PRINT AT 18,13;"ROUND:";R;" GET READY"
1060 PRINT AT 20,15;S$(N);" - ";"PRESS %R"
1064 LET C=RND
1065 IF INKEY$="" THEN GOTO 1064
1070 IF INKEY$<>"R" THEN GOTO 1070
1100 FOR A=1 TO 3
1110 LET T(A)=INT (RND*6)+1
1120 NEXT A
1130 GOSUB 5000
1150 FOR B=1 TO 2
1155 FOR Q=1 TO 3
1156 LET O=0
1158 GOSUB 9500
1160 PRINT AT 17,Q*4-4;"ROLL?"
1165 PRINT AT 18,13;"THROW NO ";B+1;":";AT 19,13;S$(N);" PLEASE SELECT";AT 20,13;"DICE TO THROW AGAIN"
1170 IF INKEY$="Y" THEN GOSUB 8000
1180 IF INKEY$="N" THEN GOSUB 8100
1185 IF O=1 THEN GOTO 1190
1187 GOTO 1170
1190 GOSUB 9500
1192 NEXT Q
1200 PRINT AT 17,0;"               "
1210 GOSUB 5000
1250 NEXT B
1300 IF T(1)=T(2) AND T(2)=T(3) THEN LET S(N,R)=T(1)+T(2)+T(3)
1310 IF S(N,R)=3 THEN LET S(N,R)=21
1320 LET W=0
1330 IF S(N,R)>=10 THEN LET W=1
1340 PRINT AT R+3,S(N,11)+2-W;S(N,R)
1500 NEXT N
1600 NEXT R
2000 FOR N=1 TO I
2003 LET S(N,11)=0
2005 PRINT AT 14,N*4-4;"\''\''\''\''"
2010 FOR R=1 TO 10
2020 LET S(N,11)=S(N,11)+S(N,R)
2030 NEXT R
2035 LET W=0
2040 IF S(N,11)>=10 THEN LET W=1
2050 IF S(N,11)>=100 THEN LET W=2
2055 IF S(N,11)>=1000 THEN LET W=3
2060 PRINT AT 15,N*4-2-W;S(N,11)
2070 NEXT N
2100 LET W$=S$(1)
2105 LET W=S(1,11)
2110 IF I=1 THEN GOTO 9000
2120 FOR N=2 TO I
2130 IF S(N,11)>W THEN LET W$=S$(N)
2140 IF S(N,11)>W THEN LET W=S(N,11)
2142 IF S(N,11)=W THEN LET W$=W$+", "+S$(N)
2145 NEXT N
2150 GOTO 9000
5000 FOR A=1 TO 3
5005 LET P=A*4-4
5007 PRINT AT 18,P;"    ";AT 19,P;"    ";AT 20,P;"    ";AT 21,P;"    "
5010 IF T(A)=1 THEN GOSUB 7100
5020 IF T(A)=2 THEN GOSUB 7200
5030 IF T(A)=3 THEN GOSUB 7300
5040 IF T(A)=4 THEN GOSUB 7400
5050 IF T(A)=5 THEN GOSUB 7500
5060 IF T(A)=6 THEN GOSUB 7600
5080 NEXT A
5200 RETURN 
7100 PRINT AT 18,P;"\ :% % % ";AT 19,P;"\ :% \':% ";AT 20,P;"\ :% % % ";AT 21,P;"\ '\''\''\''"
7110 RETURN 
7200 PRINT AT 18,P;"\ :% % \':";AT 19,P;"\ :% % % ";AT 20,P;"\ :\':% % ";AT 21,P;"\ '\''\''\''"
7210 RETURN 
7300 PRINT AT 18,P;"\ :% % \':";AT 19,P;"\ :% \':% ";AT 20,P;"\ :\':% % ";AT 21,P;"\ '\''\''\''"
7310 RETURN 
7400 PRINT AT 18,P;"\ :\':% \':";AT 19,P;"\ :% % % ";AT 20,P;"\ :\':% \':";AT 21,P;"\ '\''\''\''"
7410 RETURN 
7500 PRINT AT 18,P;"\ :\':% \':";AT 19,P;"\ :% \':% ";AT 20,P;"\ :\':% \':";AT 21,P;"\ '\''\''\''"
7510 RETURN 
7600 PRINT AT 18,P;"\ :\':% \':";AT 19,P;"\ :\':% \':";AT 20,P;"\ :\':% \':";AT 21,P;"\ '\''\''\''"
7610 RETURN 
8000 LET T(Q)=INT (RND*6)+1
8005 LET O=1
8010 PRINT AT 17,Q*4-4;" %Y%E%S "
8020 RETURN 
8100 PRINT AT 17,Q*4-4;" %N%O  "
8105 LET O=1
8110 RETURN 
9000 GOSUB 9500
9005 PRINT AT 18,15;"WINNER: ";W$;AT 19,15;"WITH ";W;" POINTS"
9010 PRINT AT 21,17;"PLAY AGAIN?"
9020 IF INKEY$="Y" THEN RUN 
9030 IF INKEY$="" THEN GOTO 9020
9050 STOP 
9500 PRINT AT 18,13;"                   ";AT 19,13;"                   ";AT 20,13;"                   ";AT 21,13;"                   "
9510 RETURN 
